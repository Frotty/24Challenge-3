package SpawnData
import LinkedList
import HashMap
import UnitObjEditing

// general class for storing the data of a spawned unit
// from this, the OE data is generated, but it can also be used ingame

/** a list of all the spawnData */
public LinkedList<SpawnData> spawnData
HashMap<int, SpawnData> unitIdToSpawnData

init
	spawnData = spawnData()
	unitIdToSpawnData = new HashMap<int, SpawnData>
	for s in spawnData
		unitIdToSpawnData.put(s.id, s)
		
public function unit.getSpawnData() returns SpawnData
	return getSpawnData(this.getTypeId())

public function getSpawnData(int unitId) returns SpawnData
	let firstLetter = unitId div (256*256*256)
	return unitIdToSpawnData.get(unitId + 256*256*256*('0' - firstLetter))
	
public function unit.isSpawnUnit() returns boolean
	let firstLetter = this.getTypeId() div (256*256*256)
	return firstLetter == 'a' or firstLetter == 'c'

public function orcUnitId(int baseId) returns int
	return baseId + 256*256*256*('a'-'0')

public function orcBuildingId(int baseId) returns int
	return baseId + 256*256*256*('b'-'0')
	
public function undeadUnitId(int baseId) returns int
	return baseId + 256*256*256*('c'-'0')
	
public function undeadBuildingId(int baseId) returns int
	return baseId + 256*256*256*('d'-'0')

public enum SpawnClass
	Attacker
	Defender
	
public enum ArmorType
	Normal
	Small
	Medium
	Large
	Fortified
	Hero
	Divine
	Unarmored
	
public enum AttackType
	Unknown
	Normal
	Pierce
	Siege
	Spells
	Chaos
	Magic
	Hero


public function ArmorType.toObjectString() returns string
	string result = "normal"
	switch this
		case Normal
			result = "normal"
		case Small
			result = "small"
		case Medium
			result = "medium"
		case Large
			result = "large"
		case Fortified
			result = "fort"
		case Hero
			result = "hero"
		case Divine
			result = "divine"
		case Unarmored
			result = "none"
	return result
	
public function AttackType.toObjectString() returns string
	string result = "unknown"
	switch this
		case Unknown
			result = "unknown"
		case Normal
			result = "normal"
		case Pierce
			result = "pierce"
		case Siege
			result = "siege"
		case Spells
			result = "spells"
		case Chaos
			result = "chaos"
		case Magic
			result = "magic"
		case Hero
			result = "hero"
	return result
	
public enum Race
	Undead
	Orc

public class SpawnData
	int id
	SpawnData upgradedFrom = null
	int baseUnitUndead
	int baseUnitOrc
	string nameUndead
	string nameOrc
	string description = "" // could also be split in undead and orc
	string iconOrc = "ReplaceableTextures\\CommandButtons\\BTNBerserkForTrolls.blp"
	string iconUndead = "ReplaceableTextures\\CommandButtons\\BTNBerserkForTrolls.blp"
	string hotkey = "X"
	int maxHP = 100
	int maxMana = 0
	real attackCooldown = 1.3
	int movespeed = 250
	int damageMin = 10
	int damageMax = 12
	AttackType attackType = Pierce
	ArmorType armorType = Normal
	int armor = 3
	SpawnClass spawnClass = Attacker
	string productionBuildingModelOrc = "buildings\\human\\Farm\\Farm.mdl"
	real productionBuildingScaleOrc = 0.7
	string productionBuildingModelUndead = "buildings\\human\\Farm\\Farm.mdl"
	real productionBuildingScaleUndead = 0.7
	int goldCost = 50
	int unitBuildTime = 30
	int range = 100
	string abilities = ""
	
	construct(int id, int baseUnitUndead, string nameUndead, int baseUnitOrc, string nameOrc)
		this.id = id
		this.baseUnitOrc = baseUnitOrc
		this.nameOrc = nameOrc
		this.baseUnitUndead = baseUnitUndead
		this.nameUndead = nameUndead

	/** override this callback if you want to change other fields of the unit */ 
	function additionalChanges(UnitDefinition _ud, Race _r)
		
	/** generate a nice description of this unit and its stats .... */
	function getExtendedDescription() returns string
		return "|cffeeeeee" + description + "|r\n\n"
			+ "Damage: " + I2S(damageMin) + " - " + I2S(damageMax) + "\n"
			+ "HP: " + I2S(maxHP) + "\n"




	
public function spawnData() returns LinkedList<SpawnData>
	let l = new LinkedList<SpawnData>
	l.add(new Ghoul)
	let archer = new Archer
	l.add(archer)
	let mcArcher = new MultishotArcher
	l.add(mcArcher)
	l.add(new NoviceCaster)
	l.add(new Tank)
	
	// upgrade relations:
	mcArcher.upgradedFrom = archer
	
	return l

// here are the different unit classes.
// the name of the class is always the unit name of the undeads

class Ghoul extends SpawnData
	construct()
		super('0gho', 'ugho', "Ghoul", 'ogru', "Grunt")

		productionBuildingModelUndead = "buildings\\undead\\Crypt\\Crypt.mdl"
		iconUndead = "ReplaceableTextures\\CommandButtons\\BTNGhoul.blp"

		productionBuildingModelOrc = "buildings\\orc\\OrcBarracks\\OrcBarracks.mdl"
		iconOrc = "ReplaceableTextures\\CommandButtons\\BTNGrunt.blp"
		
		description = "A cheap and weak melee unit."
		hotkey = "Q"
		armorType = Large
		attackType = Normal
		range = 90

class Archer extends SpawnData
	construct()
		super('0arc', 'nska', "Skeleton Archer", 'ohun', "Troll Spearthrower")
		
		productionBuildingModelUndead = "buildings\\other\\NerubianZiggurat\\NerubianZiggurat.mdl"
		iconUndead = "ReplaceableTextures\\CommandButtons\\BTNSkeletonArcher.blp"

		productionBuildingModelOrc = "buildings\\orc\\WatchTower\\WatchTower.mdl"
		iconOrc = "ReplaceableTextures\\CommandButtons\\BTNHeadhunter.blp"
		
		description = "An agressive ranged unit."
		hotkey = "W"
		armorType = Normal
		attackType = Pierce
		range = 600
		goldCost = 60
		attackCooldown = 3.
		damageMin = 20
		damageMax = 50

class MultishotArcher extends SpawnData
	construct()
		super('0mul', 'nska', "Mutlishot Skeleton Archer", 'ohun', "Mutlishot Troll Spearthrower")
		
		productionBuildingModelUndead = "buildings\\other\\NerubianZiggurat\\NerubianZiggurat.mdl"
		iconUndead = "ReplaceableTextures\\CommandButtons\\BTNSkeletonArcher.blp"
		productionBuildingScaleUndead = 0.8

		productionBuildingModelOrc = "buildings\\orc\\WatchTower\\WatchTower.mdl"
		iconOrc = "ReplaceableTextures\\CommandButtons\\BTNHeadhunter.blp"
		productionBuildingScaleOrc = 0.8
		
		description = "An agressive ranged unit."
		hotkey = "W"
		armorType = Normal
		attackType = Pierce
		range = 600
		goldCost = 200
		attackCooldown = 3.
		damageMin = 20
		damageMax = 50
		maxHP = 300
		abilities = "uroc"
	

			

class NoviceCaster extends SpawnData
	construct()
		super('0nov', 'hsor', "Undead Sorceress", 'ohun', "Troll Caster")
		
		productionBuildingModelUndead = "buildings\\undead\\Necropolis\\Necropolis.mdl"
		productionBuildingScaleUndead = 0.5
		iconUndead = "ReplaceableTextures\\CommandButtons\\BTNSorceress.blp"
		
		productionBuildingModelOrc = "buildings\\orc\\VoodooLounge\\VoodooLounge.mdl"
		iconOrc = "ReplaceableTextures\\CommandButtons\\BTNWitchDoctor.blp"
		
		description = "An agressive ranged unit."
		hotkey = "E"
		armorType = ArmorType.Unarmored
		attackType = AttackType.Magic
		range = 500
		goldCost = 65
		attackCooldown = 1.2
		damageMin = 15
		damageMax = 18
		
	override function additionalChanges(UnitDefinition ud, Race r)
		if r == Race.Undead
			ud.setTintingColor1Red(80)
			ud.setTintingColor2Green(80)
			ud.setTintingColor3Blue(100)
			ud.setAttack1ProjectileArt("Abilities\\Weapons\\AvengerMissile\\AvengerMissile.mdl")
		else
			ud.setAttack1ProjectileArt("Abilities\\Spells\\Orc\\Bloodlust\\BloodlustTarget.mdl")
		
class Tank extends SpawnData
	construct()
		super('0tan', 'uabo', "Abomination", 'otau', "Tauren")
		productionBuildingModelUndead = "buildings\\other\\NerubianZiggurat\\NerubianZiggurat.mdl"
		productionBuildingModelOrc = "buildings\\orc\\WatchTower\\WatchTower.mdl"
		iconOrc = "ReplaceableTextures\\CommandButtons\\BTNAbomination.blp"
		iconUndead = "ReplaceableTextures\\CommandButtons\\BTNTauren.blp"
		
		description = "A tank with a lot of hit points."
		hotkey = "R"
		armorType = Large
		attackType = Normal
		range = 120
		movespeed = 120
		goldCost = 120
		maxHP = 1000
		armor = 15
		unitBuildTime = 50

